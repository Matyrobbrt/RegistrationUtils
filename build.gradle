import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

buildscript {
	dependencies {
		classpath 'org.codehaus.groovy:groovy-all:3.0.9'
	}
}

plugins {
	id 'java-gradle-plugin'
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '0.18.0'
	id 'idea'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'org.cadixdev.licenser' version '0.6.1' apply false
	id 'com.matyrobbrt.mc.registrationutils' version '0.1.0-testing'
}

registrationUtils {
	// TODO testing should really be a separated project
	group 'com.matyrobbrt.regutils.testing'
	projects {
		TestingProject {
			type 'fabric'
			project ':TestingProject'
			mainClass 'com.matyrobbrt.regutils.testing.RegUtilsFabricTest'
		}
	}
	transformHolderLoading true
	addDependencies false
}

group = 'com.matyrobbrt.mc.registrationutils'

if (properties.hasProperty("testing")) {
	version = version + "-testing"
	println "New version: $version"
}

configurations {
	shade
	implementation.extendsFrom(shade)
}

repositories {
	maven { name = 'Forge'; url = 'https://maven.minecraftforge.net/' }
	maven { name = 'Fabric'; url = 'https://maven.fabricmc.net/' }
	maven { name = 'Sponge'; url = 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
	shade 'net.minecraftforge:artifactural:3.0.8'
	shade 'org.ow2.asm:asm:9.3'
	shade 'org.ow2.asm:asm-tree:9.3'
	shade 'me.lucko:jar-relocator:1.5'

//	compileOnly 'fabric-loom:fabric-loom.gradle.plugin:0.12.21'
//	compileOnly 'org.spongepowered.gradle.plugin:org.spongepowered.gradle.plugin.gradle.plugin:2.0.2'
}

this.subprojects({
	afterEvaluate {
		final var name = project.getName()
		if (name == 'Common')
			createAndAddZipTask(name, name.toLowerCase(Locale.ROOT), {
				sourcePaths(rootProject.file('licenseheader.txt'))
				action(spec -> spec.rename('licenseheader.txt', 'com/matyrobbrt/registrationutils/LICENSE'))
			})
		else if (name != 'TestingProject')
			createAndAddZipTask(name, name.toLowerCase(Locale.ROOT))
	}
})

void createAndAddZipTask(String proj, String outName, Closure<?>... extra) {
	final var task = (ProcessResources) tasks.getByName('processResources')
	final var outPath = project.buildDir.toPath().resolve("projectszip").resolve("${outName}-sources.zip")
	final var zip = tasks.create("${outName}Zip", Zip) {
		from "${proj}/src/main/java"
		from "${proj}/src/main/resources"
		for (final Closure<?> clos : extra) {
			final var conf = new ExtraConfiguration()
			clos.delegate = conf
			clos()
			from(conf.sourcePaths, conf.action)
		}
		setDestinationDir(outPath.getParent().toFile())
		archiveName("${outName}-sources.zip")
	}
	task.dependsOn(zip)
	final var jarTask = project(":${proj}").tasks.named('jar', org.gradle.jvm.tasks.Jar).get();
	for (final Closure<?> clos : extra) {
		final var conf = new ExtraConfiguration()
		clos.delegate = conf
		clos()
		jarTask.from(conf.sourcePaths, conf.action)
	}
	task.dependsOn(jarTask)
	task.from(outPath)
	final var out = List.copyOf(jarTask.getOutputs().getFiles().getFiles()).get(0);
	task.from(out).rename(out.getName(), "${outName}.zip")
}

class ExtraConfiguration {
	Object[] sourcePaths
	Action<? extends CopySpec> action = c -> {}

	def sourcePaths(Object... sourcePaths) {
		this.sourcePaths = sourcePaths
	}
	def action(Action<? extends CopySpec> action) {
		this.action = action
	}
}

pluginBundle {
	website = 'https://github.com/Matyrobbrt/RegistrationUtils'
	vcsUrl = 'https://github.com/Matyrobbrt/RegistrationUtils'
	tags = ['minecraft', 'forge', 'fabric', 'multiloader']
}

gradlePlugin {
	plugins {
		registrationUtils {
			id = 'com.matyrobbrt.mc.registrationutils'
			displayName = 'RegistrationUtils'
			description = 'A Minecraft modding plugin providing a library for easy multiloader registration.'
			implementationClass = 'com.matyrobbrt.registrationutils.gradle.RegistrationUtilsPlugin'
		}
	}
}

publishing {
	repositories {
		maven {
			name = 'localPluginRepository'
			url = './maven'
		}
	}
}

tasks.create('relocateShadowJar', ConfigureShadowRelocation) {
	target(tasks.shadowJar)
	prefix("com.matyrobbrt.registrationutils.gradle.shade")
}

tasks.named('shadowJar', ShadowJar).configure {
	archiveClassifier.set('')
	configurations = [project.configurations.shade]
	minimize()
	dependsOn('relocateShadowJar')
	manifest.attributes(makeAttributes())
}

Map<?, ?> makeAttributes() {
	final var actualDateTime = OffsetDateTime.now(ZoneOffset.UTC).withNano(0)
	final var currentDateTime = DateTimeFormatter.ISO_OFFSET_DATE_TIME.format(actualDateTime)
	return [
		'Maven-Artifact'          : "${project.group}:${archivesBaseName}:${project.version}",
		'Specification-Title'     : archivesBaseName,
		'Specification-Vendor'    : 'Matyrobbrt',
		'Specification-Version'   : '1',
		'Implementation-Title'    : archivesBaseName,
		'Implementation-Version'  : "${project.version}",
		'Implementation-Vendor'   : 'Matyrobbrt',
		'Implementation-Timestamp': currentDateTime,
		'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
	]
}

allprojects {
	apply plugin: 'org.cadixdev.licenser'
	license {
		header = rootProject.file('licenseheader.txt')
	}
}